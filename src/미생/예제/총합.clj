(ns 미생.예제.총합
  (:use [미생.기본]
        [미생.검사]))

(함수 적용총합 [n]
  "1부터 N까지의 모든 수를 더한다"
  (적용 + (범위 (증가 n))))

(함수 재귀총합 [n]
  "1부터 N까지의 모든 수를 더한다. 재귀호출 버전"
  (만약 (= n 1)
    1
    (+ n (재귀총합 (감소 n)))))

(함수 루프총합 [n]
  "1부터 N까지의 모든 수를 더한다. 루프사용 재귀호출 버전"
  (루프 [n n
         s 1]
    (만약 (= n 1)
      s
      (재귀 (감소 n) (+ s n)))))

(함수 분할총합 [n]
  "1부터 N까지의 모든 수를 더한다. 분할정복 버전"
  (조건
    (= n 1) 1
    (홀? n) (+ n (분할총합 (감소 n)))
    :짝 (가정 [n' (/ n 2)]
          (+ (* 2 (분할총합 n'))
             (* n' n')))))

(검사정의 총합검사
  (검사 "1부터 N까지 모든 수의 합"
    (확인* [x y] (= (적용총합 x) y)
           1 1
           2 3
           3 6
           4 10
           5 15
           10 55))
  (검사 "각 버전 같은 값인지 확인"
    (가정 [영역 (범위 1 (증가 1000))]
      (확인 (= (사상 적용총합 영역)
               (사상 재귀총합 영역)
               (사상 루프총합 영역)
               (사상 분할총합 영역))))))
